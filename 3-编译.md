# 3 编译

JVM被设计用来支持Java编程语言。Oracle的JDK包含了一个从Java语言到JVM指令集的编译器，以及一个运行时系统，实现了JVM。理解编译器如何利用JVM，对于后来的编译器开发者，以及那些想要了解JVM本身的人们，是很有用的。本章中的编号章节并非规范性内容。

注意，术语“编译器”有的时候指的是从JVM指令集到特定CPU指令集的翻译器。这种翻译器之一就是即时（JIT）代码生成器，它在JVM代码被加载后生成平台相关的指令。本章并不涉及这种代码生成相关的内容，只介绍从Java语言源码到JVM指令的编译。

## 3.1 栗子格式

本章包含了大量的源代码示例，以及加了注释的JVM代码，这些JVM代码是由1.0.2版本Oracle JDK编译器生成的。这些JVM代码使用“虚拟机汇编语言”，由Oracle的`javap`工具生成，这是JDK发行版自带的工具。你自己用`javap`也能生成其它的栗子。

如果你读过汇编语言代码，那这些栗子的格式你应该会很熟悉。每条指令都是这种格式：
```
<index> <opcode> [ <operand1> [ <operand2>... ]] [<comment>]
```

`<index>`是该指令操作码在对应方法的JVM字节码数组中的索引。也可以把它认为是在该方法中的一个字节偏移量。`<opcode>`是指令操作码的助记符号，然后是零或多个`<operandN>`代表指令的操作数。`<comment>`则是放在末尾的注释内容：
```
8 bipush 100 // Push int constant 100
```

注释里面有些东西是`javap`生成的；其它的则是作者加的。每条指令前的`<index>`可能会被用作控制转移指令的目标。比如一个*goto 8*指令就可以将控制转移到索引8对应的指令上。注意，JVM控制转移指令真正的操作数实际上是对应指令操作码的地址偏移量；`javap`（本章到处都是）把这些操作数都搞成更加易读的这种偏移量的形式放到了方法里。

我们先来看这样一个操作数，它代表一个运行时常量池索引，带了一个井号，然后指令后面跟着一段注释，标明运行时常量池中被引用的元素，如下：

```
10 ldc #1 // Push float constant 100.0
```
或：
```
9 invokevirtual #4 // Method Example.addTwo(II)I
```

我们紧抓本章的主题，至于操作数的大小我们无需指明。

## 3.2 常量、本地变量与控制结构的使用

通过对各种类型的设计和加以运用，JVM代码展示了一系列通用的特性。我们首先来看这样一个栗子，这个栗子我们也经常遇到，现在我们要深挖一下。

下面的`spin`方法执行一个空循环100次：

```java
void spin() {
    int i;
    for (i = 0; i < 100; i++) {
        ; // Loop body is empty
    }
}
```

编译器可以将`spin`编译成这样：

```
0 iconst_0 // Push int constant 0
1 istore_1 // Store into local variable 1 (i=0)
2 goto 8 // First time through don't increment
5 iinc 1 1 // Increment local variable 1 by 1 (i++)
8 iload_1 // Push local variable 1 (i)
9 bipush 100 // Push int constant 100
11 if_icmplt 5 // Compare and loop if less than (i < 100)
14 return // Return void when done
```

JVM是面向栈的，大部分操作都需要从JVM当前帧的操作数栈上拿出至少一个操作数，或者是将运算结果压回操作数栈。每次调用方法时，都穿创建一个新的帧，随之也创建了一个方法所需的新的操作数栈和一个局部变量集合（§2.6）。因此，在任一时刻，每个控制线程都包含了大量的帧和同样多的操作数栈，对应着大量的方法嵌套调用。只有当前帧的操作数栈是处于活动状态的。

JVM指令集对不同数据类型的操作使用不同的字节码，从而对不同的操作数类型加以区分。上面的方法`spin`只操作了`int`类型的值。在编译后的代码中，用的指令都是`int`类型特定的指令（iconst_0, istore_1, iinc, iload_1, if_icmplt）。

`spin`中的`0`和`100`这两个常量，压栈的时候用了不同的指令。压`0`用的是*iconst_0*指令，它属于*iconst_&lt;i&gt;*指令族。压`100`用的是`bipush`指令，它把它的操作数作为一种即时操作数，提出它的值并进行压栈。

JVM频繁利用了某些高频操作数的特点（`int`常量*-1*,*0*,*1*,*2*,*3*,*4*,*5*都属于*iconst_&lt;i&gt;*），这样操作码中就可以直接隐式包含了操作数。因为*iconst_0*指令知道它要压的就是`int 0`，所以它不需要把这个值提前保存到某个操作数中，也不需要从操作数中提值或解码什么的。如果把压`0`编译成*bipush 0*也对，但是最终的`spin`代码就会多出一个字节来。如果是一个比较简易的虚拟机，每次循环可能还会花费额外的时间对显式操作数提值和解码。用了隐式操作数，编译后的代码更加紧凑高效。

`spin`中的`int i`保存为JVM的局部变量*1*。由于大部分JVM指令操作的值都是从操作数栈弹出来的，而不是直接操作局部变量，所以在局部变量和操作数栈之间来回倒腾也成为了JVM编译后代码中非常常见的场景。这些操作在指令集中也提供了特殊的支持。在`spin`中，操作数从局部变量转移时，使用了*istore_1*和*iload_1*指令，它们都是在隐式操作局部变量*1*。*istore_1*是从操作数栈弹出一个`int`然后把它保存到局部变量*1*中。*iload_1*指令则是将局部变量*1*的值压入操作数栈。

对于局部变量的使用（重用）是编译器编写者们的工作。这些特殊的读写指令可以让编译器编写者们更加灵活的对局部变量进行重用。生成的结果代码也就更快，更紧凑，占用的帧空间也更小。

JVM对这些局部变量的高频操作提供了大量的福利。*iinc*指令可以将局部变量的值递增一个单字节有符号值。`spin`中的*iinc*指令将第一个局部变量（指令的第一个操作数）递增了*1*（指令的第二个操作数）。*iinc*指令在实现循环结构的时候特别好使。

`spin`中的`for`循环主要是靠这些指令实现的：

```
5 iinc 1 1 // Increment local variable 1 by 1 (i++)
8 iload_1 // Push local variable 1 (i)
9 bipush 100 // Push int constant 100
11 if_icmplt 5 // Compare and loop if less than (i < 100)
```

*bipush*将*100*作为一个`int`压入操作数栈，*if_icmplt*则是从操作数栈中弹出这个值然后将它跟*i*比较。如果比较成功（`i`小于`100`），控制转移到索引*5*处，开始`for`循环的下一个迭代。否则继续*if_icmplt*后面的指令。

如果`spin`不用`int`类型做累加器，编译后的代码肯定也要变。比如用`double`：
```java
void dspin() {
    double i;
    for (i = 0.0; i < 100.0; i++) {
        ; // Loop body is empty
    }
}
```

编译后：
```
Method void dspin()
0 dconst_0 // Push double constant 0.0
1 dstore_1 // Store into local variables 1 and 2
2 goto 9 // First time through don't increment
5 dload_1 // Push local variables 1 and 2
6 dconst_1 // Push double constant 1.0
7 dadd // Add; there is no dinc instruction
8 dstore_1 // Store result in local variables 1 and 2
9 dload_1 // Push local variables 1 and 2
10 ldc2_w #4 // Push double constant 100.0
13 dcmpg // There is no if_dcmplt instruction
14 iflt 5 // Compare and loop if less than (i < 100.0)
17 return // Return void when done
```

现在这些指令都变成`double`专用的了。（后面会讲*ldc2_w*指令。）

我们前面讲过，`double`值要占两个局部变量的位置，访问的时候用二者中较低的索引。`long`也是如此。再看这个栗子：
```java
double doubleLocals(double d1, double d2) {
    return d1 + d2;
}
```
编译后
```
Method double doubleLocals(double,double)
0 dload_1 // First argument in local variables 1 and 2
1 dload_3 // Second argument in local variables 3 and 4
2 dadd
3 dreturn
```
注意`double`用的一对儿局部变量，永远不能拆开单独使用。

JVM操作码大小为1个字节，在编译后的代码中会变得非常紧凑。但是1字节的操作码就意味着整个JVM指令集也很小。因此JVM并没有为所有数据类型都提供相等判断：非完全正交（表2.11.1-A）。

比如上面的栗子中，`int`的相等判断可以用一个`if_icmplt`指令；但是`double`的话一个指令就不够了。索引`dspin`就要用*dcmpg*加*iflt*指令一起来实现`double`值的比较。

JVM给`int`提供的直接支持是最多的。部分原因是为了让JVM的操作数栈以及局部变量数组在实现上更加高效。而且还考虑到`int`类型在一般的程序中最为常用。其它整数类型的直接支持就很少了。比如`byte`、`char`、`short`都没有存储、加载或相加指令。下面我们把`spin`改成`short`的：
```java
void sspin() {
    short i;
    for (i = 0; i < 100; i++) {
        ; // Loop body is empty
    }
}
```
编译后操作的很有可能是`int`，然后还要在`short`和`int`之间来回转换，保证`short`类型的操作结果仍然保持在正确的范围内：
```
Method void sspin()
0 iconst_0
1 istore_1
2 goto 10
5 iload_1 // The short is treated as though an int
6 iconst_1
7 iadd
8 i2s // Truncate int to short
9 istore_1
10 iload_1
11 bipush 100
13 if_icmplt 5
16 return
```
对于`byte`、`char`、`short`类型缺少直接支持并没有什么损害，因为这些值都会被升级成`int`（`byte`和`short`做有符号扩展，char做零值扩展）。然后它们的操作就直接用`int`的指令就可以了。唯一的额外消耗就是要把`int`操作的结果截断到有效的范围内。

`long`和浮点数类型的支持度居中，只是在有条件控制转移指令上缺少一定的支持。

## 3.3 运算

JVM一般都是在栈上运算。（除了*iinc*指令，它直接递增局部变量的值。）比如下面的`align2grain`方法，将一个`int`值对齐到一个指定的2次幂：

```java
int align2grain(int i, int grain) {
    return ((i + grain-1) & ~(grain-1));
}
```

运算所需的操作数从操作数栈弹出，然后把计算结果压回到栈中。这样就便于实现各种嵌套的运算方式。比如`~(grain-1)`对应的指令是：

```
5 iload_2 // Push grain
6 iconst_1 // Push int constant 1
7 isub // Subtract; push result
8 iconst_m1 // Push int constant -1
9 ixor // Do XOR; push result
```

首先`grain-1`用局部变量*2*和一个`int`值`1`来做计算。这俩操作数要从栈上弹出然后将它们的差值结果压回到栈中。然后这个结果值就可以立刻被*ixor*指令使用。（注意`~x == -1^x`。）同理，*ixor*指令的结果也可以被后续的*iand*指令使用。

完整的代码如下：

```
Method int align2grain(int,int)
0 iload_1
1 iload_2
2 iadd
3 iconst_1
4 isub
5 iload_2
6 iconst_1
7 isub
8 iconst_m1
9 ixor
10 iand
11 ireturn
```

## 3.4 访问运行时常量池

许多数字常量，以及对象、属性和方法都是通过当前类的运行时常量池来访问的。关于对象的访问我们后面再说（§3.8）。`int`、`long`、`float`、`double`类型的数据，以及`String`类实例的引用，是通过*ldc*、*ldc_w*和*ldc2_w*指令来控制的。

*ldc*和*ldc_w*指令用来访问运行时常量池中（包括`String`类实例）除了`double`和`long`以外类型的值。当运行时常量池比较大，且访问其中某个元素时需要访问一个较大的索引时，我们用*ldc_w*而不是*ldc*。*ldc2_w*指令用来访问`double`和`long`类型的值；这里不存在非宽变型。

`byte`、`char`、`short`，还有比较小的`int`值，这些整数类型的常量可能在编译后使用*bipush*、*sipush*或*iconst_&lt;i&gt;*指令（§3.2）。某些较小的浮点数常量编译后可能会使用*fconst_&lt;f&gt;*和*dconst_&lt;d&gt;*指令。

所有这些情况，编译时的操作都显而易见了。比如下面的常量：

```java
void useManyNumeric() {
    int i = 100;
    int j = 1000000;
    long l1 = 1;
    long l2 = 0xffffffff;
    double d = 2.2;
    ...做点儿什么计算...
}
```

编译后就是：

```
Method void useManyNumeric()
0 bipush 100 // Push small int constant with bipush
2 istore_1
3 ldc #1 // Push large int constant (1000000) with ldc
5 istore_2
6 lconst_1 // A tiny long value uses small fast lconst_1
7 lstore_3
8 ldc2_w #6 // Push long 0xffffffff (that is, an int -1)
            // Any long constant value can be pushed with ldc2_w
11 lstore 5
13 ldc2_w #8 // Push double constant 2.200000
             // Uncommon double values are also pushed with ldc2_w
16 dstore 7
...做点儿什么计算...
```

## 3.5 其他控制举例

前面已经讲过`for`循环的栗子了（§3.2）。Java语言中的其它大部分控制结构（`if-then-else`、`do`、`while`、`break`、`continue`）编译后的样子也都是很显而易见的。关于`switch`语句我们单讲（§3.10），以及异常（§3.12）和`finally`语法（§3.13）。

我们再多举一个栗子吧，一个`whilw`循环，尽管JVM提供的控制转移指令会根据数据类型的不同有所变化，但`while`循环编译后的结果依然是显而易见的。同样，对于`int`类型的支持也更丰富，比如：

```java
void whileInt() {
    int i = 0;
    while (i < 100) {
        i++;
    }
}
```

编译后：

```
Method void whileInt()
0 iconst_0
1 istore_1
2 goto 8
5 iinc 1 1
8 iload_1
9 bipush 100
11 if_icmplt 5
14 return
```

注意`while`中的测试语句（使用*if_icmplt*指令）位于编译后代码的底部。（这跟之前的`spin`一样。）把测试放到底部，可以强制使用一道*goto*语句，这样在第一次循环迭代开始之前就可以做一次测试。如果测试失败，就不碰循环体，就浪费了这个额外的指令。但是`while`循环的循环体一般都是预期要执行的，而且一般都是要执行很多次的。对于后续迭代，把测试放到尾部可以在每次迭代中减少一道指令：如果放在头部，循环体最后就要跟一个*goto*指令才能返回到头部。

其它数据类型的控制结构编译也都类似，但是必须要用该数据类型所适用的指令。由于需要更多的JVM指令，就可能会导致一些低效的代码，比如：

```java
void whileDouble() {
    double i = 0.0;
    while (i < 100.1) {
        i++;
    }
}
```

编译后：

```
Method void whileDouble()
0 dconst_0
1 dstore_1
2 goto 9
5 dload_1
6 dconst_1
7 dadd
8 dstore_1
9 dload_1
10 ldc2_w #4 // Push double constant 100.1
13 dcmpg // To compare and branch we have to use...
14 iflt 5 // ...two instructions
17 return
```

每个浮点数类型都有两个比较指令：`float`类型有*fcmpl*和*fcmpg*，`double`类型有*dcmpl*和*dcmpg*。这些变形只在处理NaN时有所不同。NaN是无序的（§2.3.2），所以当其中一个操作数为NaN时，所有的浮点数比较都会失败。编译器为当前类型选择比较指令的其中一个变形，不管比较失败的原因是不是因为遇到了NaN，都能产生同样的结果。比如：

```java
int lessThan100(double d) {
    if (d < 100.0) {
        return 1;
    } else {
        return -1;
    }
}
```

编译后：

```
Method int lessThan100(double)
0 dload_1
1 ldc2_w #4 // Push double constant 100.0
4 dcmpg // Push 1 if d is NaN or d > 100.0;
        // push 0 if d == 100.0
5 ifge 10 // Branch on 0 or 1
8 iconst_1
9 ireturn
10 iconst_m1
11 ireturn
```

如果`d`不是NaN且小于`100.0`，*dcmpg*指令会往栈上压一个`int`*-1*，然后就不会走*ifge*指令的分支。如果`d`大于`100.0`或者是个NaN，*dcmpg*指令就往栈上压一个`int`*1*。如果`d`等于`100.0`，*dcmpg*则压一个`int`*0*，这两种情况都会走*ifge*的分支。

如果把比较反过来，*dcmpl*指令可以得到同样的结果：

```java
int greaterThan100(double d) {
    if (d > 100.0) {
        return 1;
    } else {
        return -1;
    }
}
```
就成了：
```
Method int greaterThan100(double)
0 dload_1
1 ldc2_w #4 // Push double constant 100.0
4 dcmpl // Push -1 if d is NaN or d < 100.0;
        // push 0 if d == 100.0
5 ifle 10 // Branch on 0 or -1
8 iconst_1
9 ireturn
10 iconst_m1
11 ireturn
```

这样，不管比较是否由于碰到了NaN失败，*dcmpl*指令都会压一个`int`值，然后走*ifle*的分支。如果没有用到任何的*dcmp*指令，上面两个栗子中肯定要有一个去做很多工作来验证NaN。

## 3.6 接收参数

假设有*n*个参数传入一个实例方法，一般是被该方法帧中编号为*1*到*n*的局部变量所接收。接受顺序和传入顺序保持一致。比如：

```java
int addTwo(int i, int j) {
    return i + j;
}
```

编译后：

```
Method int addTwo(int,int)
0 iload_1 // Push value of local variable 1 (i)
1 iload_2 // Push value of local variable 2 (j)
2 iadd // Add; leave int result on operand stack
3 ireturn // Return int result
```

一般来说，实例方法中编号为*0*的局部变量保存了指向方法所属实例的一个`reference`。在Java语言中通过`this`关键字来访问这个实例。

类（`static`）方法不存在什么实例，因此对它们来说使用编号*0*的局部变量就没什么意义了。类方法使用的局部变量从索引*0*开始。如果把`addTwo`改成类方法，参数传递跟上面类似：

```java
static int addTwoStatic(int i, int j) {
    return i + j;
}
```
编译后：
```
Method int addTwoStatic(int,int)
0 iload_0
1 iload_1
2 iadd
3 ireturn
```
不同之处仅在于方法参数从*0*开始而不是*1*。

## 3.7 方法调用

对于一个实例的常规方法调用会分派到该对象的运行时类型上。（C++里叫虚方法。）这种调用通过*invokevirtual*指令实现，它的参数是运行时常量池中元素的索引，它能拿到对象类型的二进制名称、方法名以及方法描述符（§4.3.3）。假如要调用上面写的`addTwo`方法，可以这样：

```java
int add12and13() {
    return addTwo(12, 13);
}
```
编译后：
```
Method int add12and13()
0 aload_0 // Push local variable 0 (this)
1 bipush 12 // Push int constant 12
3 bipush 13 // Push int constant 13
5 invokevirtual #4 // Method Example.addtwo(II)I
8 ireturn // Return int on top of operand stack;
          // it is the int result of addTwo()
```

首先压了一个当前实例的`reference`，即`this`。然后压方法参数，`int`值`12`和`13`。当`addTwo`方法的帧创建后，这俩参数传入方法，成为方法帧中两个局部变量的初始值。也就是说，`this`的`reference`以及俩参数是由调用者来压栈的，分别成为了被调用方法的*0*、*1*、*2*号局部变量。

最后，`addTwo`被调用。当它返回时，它的`int`返回值被压入调用者帧的操作数栈，也就是`add12and13`方法中。然后紧接着继续返回给`add12and13`的调用者。

从`add12and13`返回的时候是由它的*ireturn*指令来实现的。*ireturn*指令从当前帧的栈顶拿到了`addTwo`返回的`int`值，然后将它压到调用者帧的操作数栈上。然后将控制转交给调用者，将调用者帧转变为当前帧。JVM为不同的数值类型以及`reference`类型提供了不同的返回指令，甚至还有一个*return*指令来支持没有返回值的情况。所有类型的方法调用用的都是这一批返回指令。

*invokevirtual*指令的操作数（例子中是运行时常量池索引*#4*）并非方法在类实例中的偏移量。编译器并不知道类实例的内部布局。实际上它是生成了一个实例方法的符号引用，保存到了运行时常量池中。这些运行时常量池里面的东西会在运行时进行处理，判断出真实的方法位置。JVM中所有的类实例访问指令都是这么个道理。

下面来调用一下`addTwoStatic`，它是`addTwo`的类化（`static`）变种人X战警，也是类似，就这样：

```java
int add12and13() {
    return addTwoStatic(12, 13);
}
```
尽管这里用了不同的JVM方法调用指令：
```
Method int add12and13()
0 bipush 12
2 bipush 13
4 invokestatic #3 // Method Example.addTwoStatic(II)I
7 ireturn
```

编译的时候跟实例方法调用基本一样，只不过调用者没给传`this`。因此收到的方法参数就是从局部变量*0*开始（§3.6）。调用类方法的话就是要用*invokestatic*指令。

*invokespecial*指令必须用于调用实例初始化方法（§3.8）。同样还用于父类（`super`）方法的调用。比如有下面的`Near`和`Far`类：

```java
class Near {
    int it;
    int getItNear() {
        return it;
    }
}
class Far extends Near {
    int getItFar() {
        return super.getItNear();
    }
}
```

方法`Far.getItFar`（调用了一个父类方法）变成了这个样子：

```
Method int getItFar()
0 aload_0
1 invokespecial #4 // Method Near.getItNear()I
4 ireturn
```

注意，用*invokespecial*指令调用的方法永远会把`this`传给被调用的方法作为第一个参数。通常是接收为*0*号局部变量。

要想调用方法句柄的目标，编译器必须要搞出一个方法描述符来记录真实的参数和返回类型。编译器可以不用管方法调用时的参数转换；但是必须要把它们按照未转换的类型压栈。编译器在参数压栈之前照常先压了方法句柄对象的一个`reference`。编译器生成了一道*invokevirtual*指令，引用了一个描述符，描述了参数和返回类型。通过特殊的方法解析（§5.4.3.3）安排，*invokevirtual*指令调用`java.lang.invoke.MethodHandle`的`invokeExact`或`invoke`，保证能够成功链接，得到格式正确的方法描述符，描述符中的类型命名可以被正确的解析。

## 3.8 使用类实例

JVM的类实例用*new*指令创建。在JVM中，构造器也是一种方法，编译器给它起了个名字叫`<init>`。这种特殊的方法就叫做实例初始化方法（§2.9）。一个类中可以有多个实例初始化方法，对应多个构造器。当类实例创建后，它所有的实例变量，包括自身的和父类的，都已经被初始化成默认值了，然后会调用这个新实例的一个初始化方法。比如：

```java
Object create() {
    return new Object();
}
```
编译后：
```
Method java.lang.Object create()
0 new #1 // Class java.lang.Object
3 dup
4 invokespecial #4 // Method java.lang.Object.<init>()V
7 areturn
```

尽管`reference`类型有自己的一些指令，类实例的传递和返回（作为`reference`类型）其实跟数值很像，比如：

```java
int i; // An instance variable
MyObj example() {
    MyObj o = new MyObj();
    return silly(o);
}
MyObj silly(MyObj o) {
    if (o != null) {
        return o;
    } else {
        return o;
    }
}
```
编译后：
```
Method MyObj example()
0 new #2 // Class MyObj
3 dup
4 invokespecial #5 // Method MyObj.<init>()V
7 astore_1
8 aload_0
9 aload_1
10 invokevirtual #4 // Method Example.silly(LMyObj;)LMyObj;
13 areturn
Method MyObj silly(MyObj)
0 aload_1
1 ifnull 6
4 aload_1
5 areturn
6 aload_1
7 areturn
```

类实例的字段（实例变量）通过*getfield*和*putfield*指令来访问。假设`i`是一个`int`类型的实例变量，并有`setIt`和`getIt`两个方法，定义如下：
```java
void setIt(int value) {
    i = value;
}
int getIt() {
    return i;
}
```
编译后：
```
Method void setIt(int)
0 aload_0
1 iload_1
2 putfield #4 // Field Example.i I
5 return
Method int getIt()
0 aload_0
1 getfield #4 // Field Example.i I
4 ireturn
```

如同方法调用指令的操作数一样，*putfield*和*getfield*指令的操作数（运行时常量池中的*#4*索引）也并非字段在类实例中的偏移量。编译器生成实例字段的符号引用，保存在运行时常量池中。池中的这些元素在运行时进行解析，判断出字段在被引用对象中的位置。

## 3.9 数组

JVM中的数组也是对象。数组的创建和操作用了一套不同的指令集。*newarray*指令用来创建一个数值类型的数组。例如：

```java
void createBuffer() {
    int buffer[];
    int bufsz = 100;
    int value = 12;
    buffer = new int[bufsz];
    buffer[10] = value;
    value = buffer[11];
}
```
编译后可能是介个样子：
```
Method void createBuffer()
0 bipush 100 // Push int constant 100 (bufsz)
2 istore_2 // Store bufsz in local variable 2
3 bipush 12 // Push int constant 12 (value)
5 istore_3 // Store value in local variable 3
6 iload_2 // Push bufsz...
7 newarray int // ...and create new int array of that length
9 astore_1 // Store new array in buffer
10 aload_1 // Push buffer
11 bipush 10 // Push int constant 10
13 iload_3 // Push value
14 iastore // Store value at buffer[10]
15 aload_1 // Push buffer
16 bipush 11 // Push int constant 11
18 iaload // Push value at buffer[11]...
19 istore_3 // ...and store it in value
20 return
```
*anewarray*指令用来创建一个一维的对象引用数组，比如：

```java
void createThreadArray() {
    Thread threads[];
    int count = 10;
    threads = new Thread[count];
    threads[0] = new Thread();
}
```
编译后：
```
Method void createThreadArray()
0 bipush 10 // Push int constant 10
2 istore_2 // Initialize count to that
3 iload_2 // Push count, used by anewarray
4 anewarray class #1 // Create new array of class Thread
7 astore_1 // Store new array in threads
8 aload_1 // Push value of threads
9 iconst_0 // Push int constant 0
10 new #1 // Create instance of class Thread
13 dup // Make duplicate reference...
14 invokespecial #5 // ...for Thread's constructor
                    // Method java.lang.Thread.<init>()V
17 aastore // Store new Thread in array at 0
18 return
```
*anewarray*指令还可以用来创建多维数组的第一维。*multianewarray*可以用来一键创建多维数组。比如下面的三维数组：

```java
int[][][] create3DArray() {
    int grid[][][];
    grid = new int[10][5][];
    return grid;
}
```
编译后：
```
Method int create3DArray()[][][]
0 bipush 10 // Push int 10 (dimension one)
2 iconst_5 // Push int 5 (dimension two)
3 multianewarray #1 dim #2 // Class [[[I, a three-dimensional
                           // int array; only create the
                           // first two dimensions
7 astore_1 // Store new array...
8 aload_1 // ...then prepare to return it
```

*multianewarray*指令的第一个操作数是数组对应的类信息在运行时常量池中的索引。第二个是数组真正要创建的维数。可以看出，这条指令可以用于创建所有的维度。注意，多维数组也不过就是一个对象，所以要用*aload_1*和*areturn*来压栈和返回。关于数组类名的知识可以去看§4.4.1。

所有数组都包含长度，可以通过*arraylength*指令来访问它的长度。

## 3.10 编译switch

`swtich`语句的编译用到了*tableswitch*和*lookupswitch*指令。啥时候用*tableswtich*呢，如果`switch`可以有效的表达成目标偏移量的一系列索引，那就可以用。如果`switch`表达式得到的值掉出有效的索引范围，那就走到`default`目标。比如：

```java
int chooseNear(int i) {
    switch (i) {
        case 0: return 0;
        case 1: return 1;
        case 2: return 2;
        default: return -1;
    }
}
```
编译后：

```
Method int chooseNear(int)
0 iload_1 // Push local variable 1 (argument i)
1 tableswitch 0 to 2: // Valid indices are 0 through 2
0: 28 // If i is 0, continue at 28
1: 30 // If i is 1, continue at 30
2: 32 // If i is 2, continue at 32
default:34 // Otherwise, continue at 34
28 iconst_0 // i was 0; push int constant 0...
29 ireturn // ...and return it
30 iconst_1 // i was 1; push int constant 1...
31 ireturn // ...and return it
32 iconst_2 // i was 2; push int constant 2...
33 ireturn // ...and return it
34 iconst_m1 // otherwise push int constant -1...
35 ireturn // ...and return it
```

JVM中的*tableswitch*和*lookupswitch*指令只能用于`int`数据。因为`byte`、`char`或`short`值的操作都会被提升为`int`操作，所以`switch`表达式的结果类型如果是这些之一，都会按`int`闹。比如把`chooseNear`方法改成`short`类型的参数，生成的指令也还是跟`int`的一样。其他数值类型则必须要截成`int`才能用到`switch`里。

如果`switch`中的各种case比较分散，这个时候用*tableswitch*的话从空间上就会比较低效了。此时就可以用*lookupswitch*指令了。*lookupswitch*指令将`int`健（`case`的标签）跟目标偏移量在表中对应起来。执行*lookupswitch*指令的时候，`switch`表达式的值要跟表中的健进行比较。如果匹配到了，则从对应的目标偏移量开始执行。如果没有匹配，则从`default`执行。比如：

```java
int chooseFar(int i) {
    switch (i) {
        case -100: return -1;
        case 0: return 0;
        case 100: return 1;
        default: return -1;
    }
}
```

编译后跟`chooseFar(int)`差不多，只不过用了*lookupswitch*指令：

```
Method int chooseFar(int)
0 iload_1
1 lookupswitch 3:
-100: 36
0: 38
100: 40
default: 42
36 iconst_m1
37 ireturn
38 iconst_0
39 ireturn
40 iconst_1
41 ireturn
42 iconst_m1
43 ireturn
```

JVM规范要求*lookupswitch*指令的表中必须按照健的顺序进行排序，所以在实现时可以使用比线性查找更高效的搜索方式。尽管如此，*lookupswitch*指令必须要进行匹配搜索，而*tableswitch*只需要做一下范围检查然后直接打到表中的索引上。因此当我们考虑空间优化的时候，*tableswitch*指令要比*lookupswitch*指令更加高效。

## 3.11 对操作数栈的操作

JVM中包含了大量可以用来对操作数栈进行操作的指令，且不用操心上面的具体类型。这个蛮有用的，因为JVM要仰仗操作数栈管理的灵活性。例如：

```java
public long nextIndex() {
    return index++;
}
private long index = 0;
```
编译后：
```
Method long nextIndex()
0 aload_0 // Push this
1 dup // Make a copy of it
2 getfield #4 // One of the copies of this is consumed
// pushing long field index,
// above the original this
5 dup2_x1 // The long on top of the operand stack is
// inserted into the operand stack below the
// original this
6 lconst_1 // Push long constant 1
7 ladd // The index value is incremented...
8 putfield #4 // ...and the result stored in the field
11 lreturn // The original value of index is on top of
// the operand stack, ready to be returned
```
注意，JVM不会允许操作数栈指令去修改或拆分栈上的值。

## 3.12 抛出和处理异常

程序中用`keyword`关键字来抛出异常。编译后也很简单：

```java
void cantBeZero(int i) throws TestExc {
    if (i == 0) {
        throw new TestExc();
    }
}
```
编译后：
```
Method void cantBeZero(int)
0 iload_1 // Push argument 1 (i)
1 ifne 12 // If i==0, allocate instance and throw
4 new #1 // Create instance of TestExc
7 dup // One reference goes to its constructor
8 invokespecial #7 // Method TestExc.<init>()V
11 athrow // Second reference is thrown
12 return // Never get here if we threw TestExc
```
`try-catch`结构的编译也很直接：

```java
void catchOne() {
    try {
        tryItOut();
    } catch (TestExc e) {
        handleExc(e);
    }
}
```
编译后：
```
Method void catchOne()
0 aload_0 // Beginning of try block
1 invokevirtual #6 // Method Example.tryItOut()V
4 return // End of try block; normal return
5 astore_1 // Store thrown value in local var 1
6 aload_0 // Push this
7 aload_1 // Push thrown value
8 invokevirtual #5 // Invoke handler method:
                   // Example.handleExc(LTestExc;)V
11 return // Return after handling TestExc
Exception table:
From To Target Type
0 4 5 Class TestExc
```
仔细看一下，`try`块编译后就好像根本没有`try`一样：
```
Method void catchOne()
0 aload_0 // Beginning of try block
1 invokevirtual #6 // Method Example.tryItOut()V
4 return // End of try block; normal return
```
如果`try`块执行过程中没有抛出异常，那就跟没有`try`一样：调用`tryItOut`，然后`catchOne`返回。

`try`块之后是用来实现单条`catch`语法的JVM代码：
```
5 astore_1 // Store thrown value in local var 1
6 aload_0 // Push this
7 aload_1 // Push thrown value
8 invokevirtual #5 // Invoke handler method:
                   // Example.handleExc(LTestExc;)V
11 return // Return after handling TestExc
Exception table:
From To Target Type
0 4 5 Class TestExc
```
在`catch`中，`handleExc`的调用编译后也跟普通的方法调用没啥不同。但是`catch`的出现会让编译器生成一条异常表记录（§2.10, §4.7.3）。`catchOne`的异常表中包含了一条记录，对应`catch`所处理的参数（一个`TestExc`类的实例）。如果在`catchOne`的*0*到*4*之间抛出了某个`TestExc`类型的异常，控制流就会转移到索引*5*处，该位置实现了`catch`块的内容。如果抛出的值并非`TestExc`类的实例，此处的`catch`块是无法处理它的。此时这个值会被重新抛给`catchOne`的调用者。

一个带多`catch`的`try`：

```java
void catchTwo() {
    try {
        tryItOut();
    } catch (TestExc1 e) {
        handleExc(e);
    } catch (TestExc2 e) {
        handleExc(e);
    }
}
```
如果带了多个`catch`，编译的时候就是简单的把每个`catch`块按顺序排下来，并且在异常表中添加好对应的记录：
```
Method void catchTwo()
0 aload_0 // Begin try block
1 invokevirtual #5 // Method Example.tryItOut()V
4 return // End of try block; normal return
5 astore_1 // Beginning of handler for TestExc1;
           // Store thrown value in local var 1
6 aload_0 // Push this
7 aload_1 // Push thrown value
8 invokevirtual #7 // Invoke handler method:
                   // Example.handleExc(LTestExc1;)V
11 return // Return after handling TestExc1
12 astore_1 // Beginning of handler for TestExc2;
            // Store thrown value in local var 1
13 aload_0 // Push this
14 aload_1 // Push thrown value
15 invokevirtual #7 // Invoke handler method:
                    // Example.handleExc(LTestExc2;)V
18 return // Return after handling TestExc2
Exception table:
From To Target Type
0 4 5 Class TestExc1
0 4 12 Class TestExc2
```

在`try`块执行期间（索引位置*0*到*4*）如果抛出的值匹配到一个或多个`catch`（这个值可能是其中一个或多个参数类的实例），则我们选择第一个（最靠里）`catch`进行匹配。控制流会转移到该`catch`对应的位置上。如果抛出的值无法匹配任何`catch`，则该值被重新抛出，且不经过`catchTwo`的任何`catch`。

嵌套的`try-catch`编译后跟多个`catch`很像：
```java
void nestedCatch() {
    try {
        try {
            tryItOut();
        } catch (TestExc1 e) {
            handleExc1(e);
        }
    } catch (TestExc2 e) {
        handleExc2(e);
    }
}
```
编译后：
```
Method void nestedCatch()
0 aload_0 // Begin try block
1 invokevirtual #8 // Method Example.tryItOut()V
4 return // End of try block; normal return
5 astore_1 // Beginning of handler for TestExc1;
           // Store thrown value in local var 1
6 aload_0 // Push this
7 aload_1 // Push thrown value
8 invokevirtual #7 // Invoke handler method:
                   // Example.handleExc1(LTestExc1;)V
11 return // Return after handling TestExc1
12 astore_1 // Beginning of handler for TestExc2;
            // Store thrown value in local var 1
13 aload_0 // Push this
14 aload_1 // Push thrown value
15 invokevirtual #6 // Invoke handler method:
                    // Example.handleExc2(LTestExc2;)V
18 return // Return after handling TestExc2
Exception table:
From To Target Type
0 4 5 Class TestExc1
0 12 12 Class TestExc2
```

嵌套的`catch`仅体现在异常表中。JVM并不强制异常表记录（§2.10）的嵌套规则和顺序。但是由于`try-catch`是带结构的，对于所有的抛出异常以及方法的程序计数器值，编译器都可以按照从里往外的异常处理匹配原则对异常表进行排序。

比如`tryItOut`（索引*1*）抛出了一个`TestExc1`的实例，那么它就会被调用`handleExc1`的`catch`块捕获。即便该异常是发生在外层`catch`（捕获`TestExc2`）以及更外层的范围之内。

需要注意的是，`catch`语法的范围在"from"和"to"上是一个左闭右开区间(§4.7.3)。因此对`TestExc1`的异常处理范围并不包含*4*处的*return*指令。但是对`TestExc2`的异常处理范围则包含了索引*11*处的*return*指令。内部`catch`中的返回指令被涵盖在外部`catch`的处理范围之内。

## 3.13 编译finally

（本节假设编译器生成的`class`文件版本号小于等于50.0，因此可以使用*jsr*指令。详见§4.10.2.5。）

`try-finally`语句的编译跟`try-catch`的类似。在控制流转移到`try`语句外层之前，不论该过程是否正常或是由于异常抛出而中断，必须要先执行`finally`语法。例如：
```java
void tryFinally() {
    try {
        tryItOut();
    } finally {
        wrapItUp();
    }
}
```
编译后：
```
Method void tryFinally()
0 aload_0 // Beginning of try block
1 invokevirtual #6 // Method Example.tryItOut()V
4 jsr 14 // Call finally block
7 return // End of try block
8 astore_1 // Beginning of handler for any throw
9 jsr 14 // Call finally block
12 aload_1 // Push thrown value
13 athrow // ...and rethrow value to the invoker
14 astore_2 // Beginning of finally block
15 aload_0 // Push this
16 invokevirtual #5 // Method Example.wrapItUp()V
19 ret 2 // Return from finally block
Exception table:
From To Target Type
0 4 8 any
```

一共有四种方式可以将控制流传递到`try`语句之外：正常往下走、返回、执行`break`或`continue`、抛出异常。如果`tryItOut`执行过程中没有发生异常，控制流会使用*jsr*指令转移到`finally`块。索引*4*处的*jsr 14*指令对索引*14*处（`finally`块被编译成了内嵌的子过程）的`finally`块执行了一次“子过程调用”。当`finally`块执行完成后，*ret 2*指令将控制流返回到索引*4*的*jsr*指令之后。

再说的细一点儿，其中的子过程调用是这样的：*jsr*指令在跳走之前，先将下一条指令（索引*7*的*return*）的地址压入操作数栈。然后作为目标点的*astore_2*指令，它将栈上保存的地址存入局部变量*2*中。再然后`finally`块（本例中就是*aload_0*和*invokevirtual*指令）开始执行。假设块中正常执行，*ret*指令从局部变量*2*中取出保存的地址并将执行过程恢复到该地址。此时执行*return*指令，*tryFinally*正常返回。

带`finally`的`try`编译后会有一个特殊的异常处理器，它可以处理`try`里面抛出的任何异常。假设`tryItOut`抛了一个异常，会在`tryFinally`的异常表中搜索合适的异常处理器。此时我们发现了这个特殊的处理器，将控制流转移到索引*8*处。索引*8*处的*astore_1*指令将抛出的值保存到局部变量*1*中。紧接着*jsr*指令做子过程调用走到`finally`块。假设代码能够正常执行，索引*12*处的*aload_1*指令将抛出值压回到栈中，然后下面的*athrow*指令将该值重新抛出。

如果`try`同时带有`catch`和`finally`，那就更复杂了：

```java
void tryCatchFinally() {
    try {
        tryItOut();
    } catch (TestExc e) {
        handleExc(e);
    } finally {
        wrapItUp();
    }
}
```
编译后：
Method void tryCatchFinally()
0 aload_0 // Beginning of try block
1 invokevirtual #4 // Method Example.tryItOut()V
4 goto 16 // Jump to finally block
7 astore_3 // Beginning of handler for TestExc;
           // Store thrown value in local var 3
8 aload_0 // Push this
9 aload_3 // Push thrown value
10 invokevirtual #6 // Invoke handler method:
                    // Example.handleExc(LTestExc;)V
13 goto 16 // This goto is unnecessary, but was
           // generated by javac in JDK 1.0.2
16 jsr 26 // Call finally block
19 return // Return after handling TestExc
20 astore_1 // Beginning of handler for exceptions
            // other than TestExc, or exceptions
            // thrown while handling TestExc
21 jsr 26 // Call finally block
24 aload_1 // Push thrown value...
25 athrow // ...and rethrow value to the invoker
26 astore_2 // Beginning of finally block
27 aload_0 // Push this
28 invokevirtual #5 // Method Example.wrapItUp()V
31 ret 2 // Return from finally block
Exception table:
From To Target Type
0 4 7 Class TestExc
0 16 20 any

假设`try`正常执行，索引*4*处的*goto*指令会跳到索引*16*处的`finally`子过程调用。然后执行索引*26*处的`finally`块，最后控制流返回到索引*19*处的*return*指令，`tryCatchFinally`正常返回。

如果`tryItOut`抛出了一个`TestExc`的实例，异常表中第一个（最靠里）合适的异常处理器被选中用来处理该异常。异常处理代码从索引*7*处开始，将抛出值传给`handleExc`，然后在正常返回的情况下同样是执行子过程调用索引*26*处的`finally`块。如果`handleExc`未抛出异常，`tryCatchFinally`正常返回。

如果`tryItOut`抛出的值不是`TestExc`的实例，或者`handleExc`本身也抛出了一个异常，此时由异常表中的第二条记录处理，它能够处理索引*0*到*16*之间的任意抛出值。这个异常处理器将控制流转移到索引*20*处，然后首先将抛出值保存到局部变量*1*中。然后调用索引*26*处的`finally`子过程块。如果正常返回，则从局部变量*1*中取回抛出值并通过*athrow*指令重新抛出。如果在`finally`执行过程中抛出了新的值，则`finally`执行被中断，`tryCatchFinally`做中断返回，将新的值抛给它的调用者。

## 3.14 同步

JVM实现同步的方式是监视器的进入和退出，可以用显式（*monitorenter*和*monitorexit*指令）或隐式（方法调用和返回指令）的方式进行。

对于Java语言来说，最常见的同步形式就是`synchronized`方法了。`synchronized`方法的实现并没有使用常见的*monitorenter*和*monitorexit*指令。它是直接根据运行时常量池中的`ACC_SYNCHRONIZED`标记来区分的，方法调用指令（§2.11.10）会检查这个标记。

`synchronized`语句在编译后会使用*monitorenter*和*monitorexit*指令。例如：
```java
void onlyMe(Foo f) {
    synchronized(f) {
        doSomething();
    }
}
```
编译后：
```
Method void onlyMe(Foo)
0 aload_1 // Push f
1 dup // Duplicate it on the stack
2 astore_2 // Store duplicate in local variable 2
3 monitorenter // Enter the monitor associated with f
4 aload_0 // Holding the monitor, pass this and...
5 invokevirtual #5 // ...call Example.doSomething()V
8 aload_2 // Push local variable 2 (f)
9 monitorexit // Exit the monitor associated with f
10 goto 18 // Complete the method normally
13 astore_3 // In case of any throw, end up here
14 aload_2 // Push local variable 2 (f)
15 monitorexit // Be sure to exit the monitor!
16 aload_3 // Push thrown value...
17 athrow // ...and rethrow value to the invoker
18 return // Return in the normal case
Exception table:
From To Target Type
4 10 13 any
13 16 13 any
```

编译器保证在方法调用完成的时候，从方法调用开始的每一个*monitorenter*指令都有一个对应的*monitorexit*指令被执行。不论方法调用正常结束（§2.6.4）或被中断（§2.6.5）。为了强制将*monitorenter*和*monitorexit*指令在中断情况下完成配对，编译器生成了可以匹配任意异常的异常处理器（§2.10），对应的代码会执行需要的*monitorexit*指令。

## 3.15 注解

注解在`class`文件中的格式在§4.7.16-§4.7.22中介绍。在这些章节中，会让大家知道如何在类、接口、属性、方法、方法参数以及类型参数声明时来表达注解，再有就是类型注解在这些场景下的使用。包级别的注解会有额外的规则，如下：

当编译器遇到一个包级别的注解，且它必须要在运行时可用，那么就会生成一个`class`文件，包含如下属性：

- 该`class`文件用来表达的是一个接口，就是说它的`ClassFile`结构中设置了`ACC_INTERFACE`和`ACC_ABSTRACT`标记（§4.1）。
- 如果`class`文件的版本号小于50.0，则不会设置`ACC_SYNTHETIC`标记；如果大于等于50.0，那就设置`ACC_SYNTHETIC`标记。
- 该接口有用包访问级别（JLS §6.6.1）。
- 该接口名称格式为内部格式（§4.2.1）：`<i>package-name</i>.package-info`。